*MOLTIPLICATORE CARRY SAVE
.option filetype=ascii
.INCLUDE ../lib/ST65LIKE_cell_library_v2020_1.net 
.INCLUDE ../lib/16nm_HP.pm
.PARAM ALIM=1.300000
.PARAM Lmin=16n
.PARAM Wmin=16n
.PARAM XXX=1
.TRAN 0.1p 820p
.subckt AND_ARRAY_SUB	0 Vdd out0 out1 out2 out3 x0 x1 x2 x3 y	XX=1
xand0	0 Vdd out0 x0 y	AND2_SUB XX=XXX
xand1	0 Vdd out1 x1 y	AND2_SUB XX=XXX
xand2	0 Vdd out2 x2 y	AND2_SUB XX=XXX
xand3	0 Vdd out3 x3 y	AND2_SUB XX=XXX
.ends 

.subckt ADD_ARRAY_SUB	0 Vdd sum0 sum1 sum2 Cout0 Cout1 Cout2 Cin0 Cin1 Cin2 a0 a1 a2 b0 b1 b2 	XX=1
	xadd0 0 Vdd sum0 cout0 cin0 a0 b0  FA_SUB XX = XXX
	xadd1 0 Vdd sum1 cout1 cin1 a1 b1  FA_SUB XX = XXX
	xadd2 0 Vdd sum2 cout2 cin2 a2 b2  FA_SUB XX = XXX
.ends 

.subckt MCS_SUB	0 Vdd	z0 z1 z2 z3 z4 z5 z6 z7 x0 x1 x2 x3 y0 y1 y2 y3 	XX=1
xand_Level0	 0 vdd	z0    0out1 0out2 0out3 x0 x1 x2 x3 y0	AND_ARRAY_SUB XX = XXX
xand_Level1	 0 vdd	1out0 1out1 1out2 1out3 x0 x1 x2 x3 y1	AND_ARRAY_SUB XX = XXX
xADD_Level2	 0 vdd	z1 2sum1 2sum2 2cout0 2cout1 2cout2 0     0     0     1out0 1out1 1out2 0out1 0out2 0out3 	ADD_ARRAY_SUB XX = XXX
xand_Level3	 0 vdd	3out0 3out1 3out2 3out3 	x0 x1 x2 x3 	y2	AND_ARRAY_SUB XX = XXX
xADD_Level4	 0 vdd	z2 4sum1 4sum2 4cout0 4cout1 4cout2 2cout0 2cout1 2cout2 3out0 3out1 3out2 2sum1 2sum2 1out3 	ADD_ARRAY_SUB XX = XXX
xand_Level5	 0 vdd	5out0 5out1 5out2 5out3 	x0 x1 x2 x3 	y3	AND_ARRAY_SUB XX = XXX
xADD_Level6	 0 vdd	z3 6sum1 6sum2 6cout0 6cout1 6cout2 4cout0 4cout1 4cout2 5out0 5out1 5out2 4sum1 4sum2 3out3 	ADD_ARRAY_SUB XX = XXX
xADD_Level7	 0 vdd	z4 z5   z6   7cout0 7cout1 z7    	0     7cout0 7cout1 	6cout0 6cout1 6cout2 	6sum1 6sum2 5out3 	ADD_ARRAY_SUB XX = XXX
	.ends 

xmcs4 0 Vcc xout0 xout1 xout2 xout3 xout4 xout5 xout6 xout7 xx0 xx1 xx2 xx3 xy0 xy1 xy2 xy3 MCS_SUB XX = XXX
VinA3 xx3 0 0
VinA2 xx2 0 0
VinA1 xx1 0 ALIM
VinA0 xx0 0 0
VinB3 xy3 0 0
VinB2 xy2 0 0
VinB1 xy1 0 0
VinB0 xy0 0 ALIM
V_dd Vcc 0 ALIM

.control
run
let k=length(time)-1
print xout0[k] xout1[k] xout2[k] xout3[k] xout4[k] xout5[k] xout6[k] xout7[k]>outputValue.txt
quit
.endc
.end
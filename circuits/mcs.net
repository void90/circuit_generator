*MOLTIPLICATORE CARRY SAVE

.option filetype=ascii

***LIBRERIE***
.INCLUDE ST65LIKE_cell_library_v2020_1.net
.INCLUDE 16nm_HP.pm

***PARAMETRI GLOBALI*** 	----> Lmin Wmin come da specifiche Mastrandrea (16nm)
.PARAM vthVARn=0.3423
.PARAM vthVARp=-0.23122
.PARAM ndepVARp=1.68e+018
.PARAM toxpVAR=5.21e-010
.PARAM Lmin=16n
.PARAM Wmin=16n
.PARAM XXX=1
.PARAM tr=50p

* x & y sono ingressi
.subckt AND_ARRAY_SUB 0 vdd out0 out1 out2 out3 x0 x1 x2 x3 y XX = 1
	xand0 0 Vdd out0 x0 y AND2_SUB XX=XXX
	xand1 0 Vdd out1 x1 y AND2_SUB XX=XXX
	xand2 0 Vdd out2 x2 y AND2_SUB XX=XXX
	xand3 0 Vdd out3 x3 y AND2_SUB XX=XXX
.ends

* ai &bi (i=0..2) sono in Adder
.subckt ADD_ARRAY_SUB 0 vdd	sum0 sum1 sum2 	cout0 cout1 cout2	cin0 cin1 cin2 	a0 a1 a2	b0 b1 b2	XX = 1
	xadd0 0 vdd sum0 cout0 cin0 b0 a0 FA_SUB XX = XXX
	xadd1 0 vdd sum1 cout1 cin1 b1 a1 FA_SUB XX = XXX
	xadd2 0 vdd sum2 cout2 cin2 b2 a2 FA_SUB XX = XXX
.ends

* Zi->Uscute moltiplicatore; c->Carry d'uscita; Mj,Nj->Fattori d'ingresso. 	-----	Con [i=0,..,7; j=0,..,3]
.subckt MCS_SUB 0 vdd z0 z1 z2 z3 z4 z5 z6 z7 x0 x1 x2 x3 y0 y1 y2 y3 XX = 1
	xand4_level0 	0 vdd 	z0 0out1 0out2 0out3 x0 x1 x2 x3 y0 AND_ARRAY_SUB XX = XXX
	xand4_level1 	0 vdd 	1out0 1out1 1out2 1out3 x0 x1 x2 x3 y1 AND_ARRAY_SUB XX = XXX
	xadd_array0 	0 vdd 	z1 0sum1 0sum2 0cout0 0cout1 0cout2 0 0 0 1out0 1out1 1out2 0out1 0out2 0out3 ADD_ARRAY_SUB XX = XXX
	xand4_level2 	0 vdd 	2out0 2out1 2out2 2out3 x0 x1 x2 x3 y2 AND_ARRAY_SUB XX = XXX
	xadd_array1 	0 vdd 	z2 1sum1 1sum2 1cout0 1cout1 1cout2 0cout0 0cout1 0cout2 2out0 2out1 2out2 0sum1 0sum2 1out3 ADD_ARRAY_SUB XX = XXX
	xand4_level3 	0 vdd 	3out0 3out1 3out2 3out3 x0 x1 x2 x3 y3 AND_ARRAY_SUB XX = XXX
	xadd_array2 	0 vdd 	z3 2sum1 2sum2 2cout0 2cout1 2cout2 1cout0 1cout1 1cout2 3out0 3out1 3out2 1sum1 1sum2 2out3 ADD_ARRAY_SUB XX = XXX
	xadd_level3 	0 vdd 	z4 z5 z6 3cout0 3cout1 z7 0 3cout0 3cout1 2cout0 2cout1 2cout2 2sum1 2sum2 3out3 ADD_ARRAY_SUB XX = XXX
.ends

***DICHIARAZIONE DEL COMPONENTI***
xmcs4 0 vcc xz0 xz1 xz2 xz3 xz4 xz5 xz6 xz7 xx0 xx1 xx2 xx3 xy0 xy1 xy2 xy3 MCS_SUB XX = XXX

***COMANDI DI SIMULAZIONE***

.TRAN 0.1p 820p

**Alimentazione
V_dd Vcc 0 1
**Segnale
V0 xx0 0 PWL(0 0 5p 0 6p 1 820p 1)
V1 xx1 0 PWL(0 0 5p 0 6p 1 820p 1)
V2 xx2 0 PWL(0 0 5p 0 6p 1 820p 1)
V3 xx3 0 PWL(0 0 5p 0 6p 1 820p 1)
V4 xy0 0 PWL(0 0 5p 0 6p 1 820p 1)
V5 xy1 0 PWL(0 0 5p 0 6p 1 820p 1)
V6 xy2 0 PWL(0 0 5p 0 6p 1 820p 1)
V7 xy3 0 PWL(0 0 5p 0 6p 1 820p 1)
.end

.control
run
plot xz0 xz1 
plot xz2 xz3
plot xz4 xz5
plot xz6 xz7
.endc


.end
